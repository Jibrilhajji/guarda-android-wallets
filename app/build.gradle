apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'maven'
apply plugin: 'com.google.protobuf'

android {
    compileSdkVersion 28
    lintOptions {
        checkReleaseBuilds false
        ignoreWarnings true
    }
    defaultConfig {
        applicationId "com.guarda.ethereum"
        minSdkVersion 18
        targetSdkVersion 28
        versionCode 51
        versionName "0.61"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true

        externalNativeBuild {
            cmake {
                cppFlags "-std=c++11"
            }
        }

        ndk.abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86_64', 'x86'

        // used by Room, to test migrations
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }
    }

    signingConfigs {
        release {
            def keystoreProperties = new Properties()
            keystoreProperties.load(new FileInputStream(rootProject.file("local/release.properties")))
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storePassword keystoreProperties['storePassword']
            storeFile file(keystoreProperties['storeFile'])
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    testOptions {
        unitTests.includeAndroidResources = true
    }

    dexOptions {
        javaMaxHeapSize "4G"
    }

    flavorDimensions "tier"

    productFlavors {

        zec {
            dimension "version"
            versionCode 83
            versionName "1.43"
            applicationId "guarda.shielded"
            dimension "tier"
            setProperty("archivesBaseName", "guarda-shielded-$versionName")
        }

    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }

}

protobuf {
    protoc { artifact = 'com.google.protobuf:protoc:3.12.3' }
    plugins {
        grpc { artifact = 'io.grpc:protoc-gen-grpc-java:1.30.2' // CURRENT_GRPC_VERSION
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java { option 'lite' }
            }
            task.plugins {
                grpc { // Options added to --grpc_out
                    option 'lite' }
            }
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.gridlayout:gridlayout:1.0.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.annotation:annotation:1.1.0'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'com.jakewharton:butterknife:10.2.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.1'
    implementation 'com.github.clans:fab:1.6.4'
    implementation 'com.google.dagger:dagger:2.27'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.27'
    implementation 'com.squareup.retrofit2:retrofit:2.6.1'
    implementation 'com.squareup.retrofit2:converter-gson:2.6.1'
    implementation("com.squareup.okio:okio:2.6.0")
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.2.1'

    debugImplementation 'com.amitshekhar.android:debug-db:1.0.6'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.6.3'

    implementation files('libs/jsonrpc2-base-1.38.jar')
    implementation 'com.google.firebase:firebase-core:17.5.0'
    implementation 'com.google.firebase:firebase-crashlytics:17.2.1'
    implementation 'de.adorsys.android:securestoragelibrary:1.0.2'
    implementation 'com.hbb20:ccp:2.3.4'
    implementation 'com.github.freshdesk:freshchat-android:1.2.1'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'com.thoughtbot:expandablerecyclerview:1.3'

    implementation ('androidx.browser:browser:1.2.0') {
        exclude group: 'com.google.guava', module: 'listenablefuture'
    }
    implementation 'com.github.delight-im:Android-AdvancedWebView:v3.0.0'

    implementation 'io.grpc:grpc-okhttp:1.30.2'
    implementation 'io.grpc:grpc-protobuf-lite:1.30.2'
    implementation 'io.grpc:grpc-stub:1.30.2'
    implementation 'org.apache.tomcat:annotations-api:6.0.53' // necessary for Java 9+

    implementation 'com.dlazaro66.qrcodereaderview:qrcodereaderview:2.0.3'
    //Glide
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'
    implementation 'com.caverock:androidsvg-aar:1.4'
    implementation 'jp.wasabeef:glide-transformations:4.0.0'

    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    testImplementation 'junit:junit:4.12'

    //zec
    zecImplementation('org.bitcoinj:bitcoinj-core:0.14.5') {
        exclude group: 'com.google.protobuf', module: 'protobuf-java'
    }
    zecImplementation 'com.madgag.spongycastle:core:1.58.0.0'
    zecImplementation 'com.getkeepsafe.relinker:relinker:1.3.1'
    zecImplementation 'com.github.RobertApikyan:SegmentedControl:1.2.0'
    zecImplementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    zecImplementation 'io.reactivex.rxjava2:rxjava:2.2.6'
    zecImplementation 'com.squareup.retrofit2:adapter-rxjava2:2.6.1'
    implementation "com.squareup.okhttp3:logging-interceptor:4.7.2"
    zecImplementation 'androidx.room:room-runtime:2.2.4'
    zecAnnotationProcessor 'androidx.room:room-compiler:2.2.4'
    zecImplementation "com.goterl.lazycode:lazysodium-android:3.6.3@aar"
    zecImplementation "net.java.dev.jna:jna:5.3.1@aar"
    zecImplementation 'com.facebook.stetho:stetho:1.5.1'

    zecImplementation project(path: ':rust-lib-release')
    //lifecycle
    zecImplementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    zecAnnotationProcessor 'androidx.lifecycle:lifecycle-compiler:2.2.0'
    //tests
    androidTestZecImplementation 'androidx.room:room-testing:2.2.4'
    androidTestZecImplementation 'androidx.test:runner:1.2.0'
    androidTestZecImplementation 'androidx.test:rules:1.2.0'
    androidTestZecImplementation 'androidx.test:core:1.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
}

apply plugin: 'com.google.gms.google-services'
